<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
    xmlns:s="library://ns.adobe.com/flex/spark" xmlns:ria="spark.controls.*"
    creationComplete="init()">
    <s:layout>
        <s:VerticalLayout paddingTop="10" paddingLeft="5" paddingRight="5" />
    </s:layout>
        
    <fx:Script>
        <![CDATA[
            import spark.events.IndexChangeEvent;
            import flash.utils.*;
            import mx.collections.ArrayCollection;
            import com.airsmth.defines.*;
            import com.airsmth.renders.*;
            import com.airsmth.views.*;
            private var cnt_loaded:Number;
            private var cnt_all:Number;
            private var last_index:Number = -1;
            
            protected function init():void {
                if (data is ArrayCollection) {
                    postList.dataProvider = data[1] as ArrayCollection;
                    lbSubTitle.text = (data[0] as SearchOption).title;
                    return;
                }
                loadSearch(data as SearchOption);
            }
            
            protected function loadSearch(so:SearchOption):void {
                lbInfo.visible = true;
                var sl:SearchPostLoader = new SearchPostLoader(so);
                sl.addEventListener(LoadEvent.DONE, onLoadSearch);
                sl.load();
            }
            
            protected function onLoadSearch(event:Event):void {
                lbInfo.visible = false;
                var sl:SearchPostLoader = event.currentTarget as SearchPostLoader;
                data = new ArrayCollection([sl.option, sl.data]);
                postList.dataProvider = sl.data;
                cnt_all = sl.data.length;
                cnt_loaded = 0;
                for (var i:Number = 0; i < sl.data.length; i++) {
                    loadPost(sl.data.getItemAt(i) as Post, i);
                }
                lbSubTitle.text = sl.option.title;
            }
            
            protected function loadPost(post:Post, idx:Number):void {
                var pl:PostLoader = new PostLoader(post, idx, parentApplication.globalSMTH.config);
                pl.addEventListener(LoadEvent.DONE, loadPostDone);
                pl.load();
            }
            
            protected function loadPostDone(event:Event):void {
                var pl:PostLoader = event.currentTarget as PostLoader;
                var idx:Number = pl.idx;
                var po:Post = pl.post;
                
                //actually this is a bug, this just avoid the exception, 
                //but may happens as the post is inserted to wrong subject
                if (idx < postList.dataProvider.length) { 
                    postList.dataProvider.setItemAt(po, idx);
                    cnt_loaded = cnt_loaded + 1;
                    lbLoaded.text = cnt_loaded.toString() + "/" + cnt_all.toString();
                    data.setItemAt(postList.dataProvider, 1);
                }
            }
            
            
            protected function postClick(event:IndexChangeEvent):void {
                if (event.newIndex != -1) replyBtn.enabled = parentApplication.globalSMTH.isLoggedOn;
                if (parentApplication.globalSMTH.config.showrefer) {
                    if (event.oldIndex != -1) postList.dataProvider[event.oldIndex].showrefer = true;
                    postList.dataProvider[event.newIndex].showrefer = false;
                }
            }
            
            protected function expandall():void {
                for each(var post:Post in postList.dataProvider) {
                    post.showrefer = true;
                    
                }
            }

            
            protected function forward():void {
                var fw:Forward;
                if (parentApplication.globalSMTH.config.directforward && parentApplication.globalSMTH.config.defaulttarget != "") {
                    fw = new Forward(postList.selectedItem as Post);
                    fw.target = parentApplication.globalSMTH.config.defaulttarget;
                    lbInfo.text = "正在转寄，请稍候 ...";
                    lbInfo.visible= true;
                    var fs:ForwardSender = new ForwardSender(fw);
                    fs.addEventListener(LoadEvent.DONE, onForward);
                    fs.addEventListener(LoadEvent.LOADFAIL, onForwardFail);
                    fs.load();
                    return;
                }
            
                fw = new Forward(postList.selectedItem as Post);
                navigator.pushView(ForwardView, fw);
            }
            
            protected function onForward(event:Event):void {
                lbInfo.text = "转寄成功";
                setTimeout(function ():void {
                    lbInfo.visible = false;
                    lbInfo.text = "正在加载，请稍候 ...";
                    }, 2000);
            }
            
            protected function onForwardFail(event:Event):void {
                lbInfo.text = "转寄失败";
                setTimeout(function ():void {
                    lbInfo.visible = false;
                    lbInfo.text = "正在加载，请稍候 ...";
                    }, 4000);
            }
            
            protected function replymail():void {
                var para:URLVariables = new URLVariables();
                para.board = (postList.selectedItem as Post).bname;
                para.id = (postList.selectedItem as Post).id;
                navigator.pushView(MailEditView, para);
            }
            
            protected function menuOpen():void {
                parentApplication.viewMenuOpen=true;
            }

            
        ]]>
    </fx:Script>
    <s:navigationContent>
        <s:Button icon="@Embed(source='../../../assets/back.png')" click="navigator.popView()" id="btnBack"/>
    </s:navigationContent>
    <s:actionContent>
        <s:Button icon="@Embed(source='../../../assets/reply_post.png')" 
            click="navigator.pushView(ReplyView, postList.selectedItem)" id="replyBtn" enabled="false" />
        <s:Button icon="@Embed(source='../../../assets/more.png')" click="menuOpen()" id="moreBtn" />
    </s:actionContent>
    <s:HGroup width="100%">
    <s:Label id="lbSubTitle" styleName="subjecttitle"/>
    <s:Label id="lbLoaded"  textAlign="end" width="100%" styleName="postindex"/>
    </s:HGroup>
    <s:List id="postList"
        width="100%" height="100%" 
        itemRendererFunction="RenderUtil.selectRenderer"
        change="postClick(event)" />
    <s:Label id="lbInfo" styleName="loading" text="正在加载，请稍候 ..." visible="false"/>
    <s:viewMenuItems>
        <s:ViewMenuItem click="expandall()"  label="全部展开"
            icon="@Embed(source='../../../assets/post_expand.png')" enabled="{!(parentApplication.globalSMTH.config.showrefer)}" />
        <s:ViewMenuItem label="回信"
            icon="@Embed(source='../../../assets/post_reply_mail.png')" click="replymail()" enabled="{parentApplication.globalSMTH.isLoggedOn}" />
        <s:ViewMenuItem id="meTrash" label="删除"
            icon="@Embed(source='../../../assets/trash.png')" enabled="false" />
        <s:ViewMenuItem label="转寄"
            icon="@Embed(source='../../../assets/post_forward.png')" click="forward()" enabled="{parentApplication.globalSMTH.isLoggedOn}" />
    </s:viewMenuItems>
</s:View>