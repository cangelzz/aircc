<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
    xmlns:s="library://ns.adobe.com/flex/spark" 
    creationComplete="init()">
    <s:layout>
        <s:VerticalLayout paddingTop="5" paddingLeft="5" paddingRight="5"/>
    </s:layout>
        
    <fx:Script>
        <![CDATA[
            import mx.collections.ArrayCollection;
            import com.airsmth.defines.*;
            import com.airsmth.views.*;
            import com.airsmth.renders.*;
            public var deleted:Boolean = false;
            
            protected function init():void {
                if (data is ArrayCollection) {
                    setPageUI(data[0] as Mail);
                    return;
                }
                var ml:MailLoader = new MailLoader(data as Mail);
                ml.addEventListener(LoadEvent.DONE, onLoad);
                ml.load();
            }
            
            protected function onLoad(event:Event):void {
                var m:Mail = (event.currentTarget as MailLoader).mail;
                setPageUI(m);
                data = new ArrayCollection([m]);
            }
            
            protected function setPageUI(m:Mail):void {
                if (m.realauthor != null) {
                    lbRealAuthor.text = m.realauthor;
                    lbRealAuthor.includeInLayout = true;
                }
                lbAuthor.text = m.author;
                lbContent.text = m.content;
                lbInfo.visible = false;
            }
            
            protected function replymail():void {
                navigator.pushView(MailEditView, data[0].createReply())
            }
            
            protected function delmail():void {
                var dl:DelMailLoader = new DelMailLoader(data[0] as Mail);
                dl.addEventListener(LoadEvent.DONE, onDelete);
                dl.load();
            }
            
            protected function onDelete(event:Event):void {
                deleted = true;
                navigator.popView();
            }
            
            override public function createReturnObject():Object {
                return deleted;
            } 
            
        ]]>
    </fx:Script>
    <s:navigationContent>
        <s:Button icon="@Embed(source='../../../assets/back.png')" click="navigator.popView()" />
    </s:navigationContent>
    <s:titleContent>
        <s:Label id="lbInfo" width="100%" styleName="titlecenter" text="正在加载，请稍候 ..."/>
    </s:titleContent>
    <s:actionContent>
        <s:Button icon="@Embed(source='../../../assets/delete.png')" id="delBtn" click="delmail()" />
        <s:Button icon="@Embed(source='../../../assets/reply_mail.png')" id="newBtn" click="replymail()" />
    </s:actionContent>
    <s:VGroup width="100%" paddingLeft="5" paddingRight="5">
    <s:Label id="lbAuthor" styleName="postauthor" />
    <s:Label id="emptyline"/>
    <s:Label id="lbRealAuthor" styleName="postlz" includeInLayout="false"/>
    <s:Label id="lbContent" width="100%" />
    </s:VGroup>

</s:View>